import os
Import('sfwprofile')
from building import *

cwd = GetCurrentDir()
group = []

src_utilitie = Split('''
MIMXRT1011/utilities/fsl_assert.c
MIMXRT1011/utilities/debug_console/fsl_debug_console.c
MIMXRT1011/utilities/str/fsl_str.c
''')
path = [cwd + '/CMSIS/Include', cwd + '/MIMXRT1011/utilities']
path += [cwd + '/MIMXRT1011/utilities/debug_console', cwd + '/MIMXRT1011/utilities/str']
group = DefineGroup('utilitie', src_utilitie, depend = [''], CPPPATH = path)

src_device = Split('''
MIMXRT1011/system_MIMXRT1011.c
''')
if sfwprofile.CROSS_TOOL == 'gcc':
    src_device += ['MIMXRT1011/gcc/startup_MIMXRT1011.S']
elif sfwprofile.CROSS_TOOL == 'keil':
    src_device += ['MIMXRT1011/arm/startup_MIMXRT1011.S']
elif sfwprofile.CROSS_TOOL == 'iar':
    src_device += ['MIMXRT1011/iar/startup_MIMXRT1011.s']
path = [cwd + '/CMSIS/Include',cwd + '/MIMXRT1011']
group =group + DefineGroup('device', src_device, depend = [''], CPPPATH = path)

src_drivers = Split('''
MIMXRT1011/drivers/fsl_common.c
MIMXRT1011/drivers/fsl_clock.c
MIMXRT1011/drivers/fsl_cache.c
''')

if GetDepend(['SOC_GPIO']):
    src_drivers += ['MIMXRT1011/drivers/fsl_gpio.c']

if GetDepend(['SOC_LPUART']):
    src_drivers += ['MIMXRT1011/drivers/fsl_lpuart.c']

if GetDepend(['SOC_I2C']):
    src_drivers += ['MIMXRT1011/drivers/fsl_lpi2c.c']

if GetDepend(['SOC_SPI']):
    src_drivers += ['MIMXRT1011/drivers/fsl_lpspi.c']

if GetDepend(['SOC_HWTIMER']) or GetDepend(['SOC_PWM']):
    src_drivers += ['MIMXRT1011/drivers/fsl_gpt.c']

if GetDepend(['SOC_RTC']):
    src_drivers += ['MIMXRT1011/drivers/fsl_snvs_hp.c']
    src_drivers += ['MIMXRT1011/drivers/fsl_snvs_lp.c']

if GetDepend(['SOC_WDT']):
    src_drivers += ['MIMXRT1011/drivers/fsl_wdog.c']
    src_drivers += ['MIMXRT1011/drivers/fsl_rtwdog.c']

if GetDepend(['SOC_FLEXSPI']):
    src_drivers += ['MIMXRT1011/drivers/fsl_flexspi.c']

path = [cwd + '/CMSIS/Include', cwd + '/MIMXRT1011', cwd + '/MIMXRT1011/drivers']
group =group + DefineGroup('driver', src_drivers, depend = [''], CPPPATH = path)

Return('group')
